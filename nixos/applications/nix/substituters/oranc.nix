{
  lib,
  pkgs,
  config,
  flake-inputs,
  ...
}:
{
  imports = [ flake-inputs.oranc.nixosModules.oranc ];

  # generated by following https://github.com/linyinfeng/oranc#direct-push
  sops.secrets."oranc/priv_key" = {
    owner = config.users.users.rithvij.name;
  };
  sops.secrets."oranc/pub_key" = { };
  sops.secrets."oranc/oci_repo2" = { };

  sops.templates.oranc_extra_nix_conf = {
    content = ''
      extra-substituters = http://localhost:9999/ghcr.io/phanirithvij/${
        config.sops.placeholder."oranc/oci_repo2"
      }
      extra-trusted-public-keys = ${config.sops.placeholder."oranc/pub_key"}
    '';
    # let every one use it as a substituter? but without auth what's the use?
    # maybe per user auth key should be provided in real scenaros by the admin
    group = "users";
  };
  sops.templates.oranc_netrc = {
    content = ''
      machine localhost
          login phanirithvij
          password ${config.sops.placeholder."github_pat"}
    '';
    # gha token can't be accessible for everyone in users only me
    owner = config.users.users.rithvij.name;
  };

  services.oranc = {
    enable = true;
    package = pkgs.nurPkgs.flakePkgs.oranc; # cached in cachix
    listen = "[::]:9999";
  };
  environment.systemPackages =
    let
      orancWithSecrets = pkgs.writeShellScriptBin "oranc" ''
        set -eou pipefail

        export ORANC_USERNAME=phanirithvij
        export ORANC_PASSWORD="$(<${config.sops.secrets."github_pat".path})"
        export ORANC_SIGNING_KEY="$(<${config.sops.secrets."oranc/priv_key".path})"

        ${lib.getExe pkgs.nurPkgs.flakePkgs.oranc} "$@"
      '';
    in
    [ orancWithSecrets ];

  nix.settings = {
    substituters = [ "http://localhost:9999/ghcr.io/phanirithvij/oranc-test" ];
    trusted-public-keys = [ "oranc-test-key:JHXX81LAjQ1talEpzGDsCohw040znVA8xSAG0YXnG/o=" ];
  };
  # TODO nix only seems to accept one netrc-file
  # can test it by having one set in /etc/nix/nix.conf
  # a different one set in ~/.config/nix/nix.conf
  # need to merge it somehow, and what about when multiple users need it
  # eg https://github.com/zakuciael/nixos-dotfiles/blob/8fbf66636a82c711bf45ba8ea868db7317514854/configuration.nix#L174
  /*
    nix.extraOptions = ''

      # extras for oranc
      !include ${config.sops.templates.oranc_extra_nix_conf.path}
      netrc-file = ${config.sops.templates.oranc_netrc.path}
    '';
  */
}
